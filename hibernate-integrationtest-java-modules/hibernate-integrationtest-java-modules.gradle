/*
 * SPDX-License-Identifier: Apache-2.0
 * Copyright Red Hat Inc. and Hibernate Authors
 */

plugins {
	id( "local.java-module" )
}

final Boolean skipJacoco = project.hasProperty("skipJacoco") ? project.property("skipJacoco").toString().toBoolean() : false
if (!skipJacoco) {
	plugins.apply("jacoco")
}

description = "Integration tests for running Hibernate ORM in the Java module path"

// See https://docs.gradle.org/6.7.1/userguide/java_testing.html#blackbox_integration_testing
// See https://docs.gradle.org/6.7.1/samples/sample_java_modules_multi_project_with_integration_tests.html
java.modularity.inferModulePath = true


dependencies {
	api( projects.hibernateCore )
	api( projects.hibernateEnvers )
	implementation( projects.hibernateScanJandex )
	//Provide the jakarta.cdi module, as it's required by module jakarta.transaction
	//but not provided as transitive dependency of Narayana.
	testRuntimeOnly( libs.jakarta.cdi )
	testRuntimeOnly( libs.jakarta.interceptors )
}

tasks.withType( Test ).configureEach {
    useJUnitPlatform()
}

