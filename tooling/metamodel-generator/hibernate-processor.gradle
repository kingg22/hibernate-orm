/*
 * SPDX-License-Identifier: Apache-2.0
 * Copyright Red Hat Inc. and Hibernate Authors
 */

plugins {
    id("local.publishing-java-module")
    id("local.publishing-group-relocation")
    id("org.hibernate.build.version-injection")
}

description = "Hibernate compile-time tooling"

sourceSets {
    register("quarkusOrmPanache") {
        java {
            setSrcDirs( ["src/quarkusOrmPanache/java"] )
        }
        resources {
            setSrcDirs(sourceSets.main.resources.srcDirs)
        }
        compileClasspath += sourceSets.main.output + sourceSets.test.output
        runtimeClasspath += sourceSets.main.output + sourceSets.test.output
    }
    register("quarkusHrPanache") {
        java {
            setSrcDirs( [ "src/quarkusHrPanache/java" ] )
        }
        resources {
            setSrcDirs(sourceSets.main.resources.srcDirs)
        }
        compileClasspath += sourceSets.main.output + sourceSets.test.output
        runtimeClasspath += sourceSets.main.output + sourceSets.test.output
    }
    register("jakartaData") {
        java {
            setSrcDirs(["src/jakartaData/java"])
        }
        resources {
            setSrcDirs(sourceSets.main.resources.srcDirs)
        }
        compileClasspath += sourceSets["main"].output + sourceSets["test"].output
        runtimeClasspath += sourceSets["main"].output + sourceSets["test"].output
    }
}

dependencies {
    // api - ewww... but Maven needs them this way
    api( projects.hibernateCore )
    api( libs.hibernateModels )
    api( libs.jandex )
    api( libs.jakarta.jaxbApi )
    api( libs.jakarta.jaxb )
    api( libs.jakarta.validation )
    api( libs.jakarta.annotation )
    api( libs.antlrRuntime )
    api( libs.byteBuddy )
    api( libs.logging )

    quarkusOrmPanacheImplementation("io.quarkus:quarkus-hibernate-orm-panache:3.24.1")
    quarkusHrPanacheImplementation("io.quarkus:quarkus-hibernate-reactive-panache:3.24.1")
    jakartaDataImplementation("jakarta.data:jakarta.data-api:1.0.0")
    jakartaDataImplementation("org.hibernate.reactive:hibernate-reactive-core:3.0.1.Final")
    jakartaDataImplementation("io.quarkus:quarkus-hibernate-orm-panache:3.24.1")
}

configurations {
    named("quarkusOrmPanacheImplementation") {
        extendsFrom( configurations.testImplementation )
    }
    named("quarkusOrmPanacheRuntimeOnly") {
        extendsFrom( configurations.testRuntimeOnly )
    }
    named("quarkusOrmPanacheCompileOnly") {
        extendsFrom( configurations.testCompileOnly )
    }

    named("quarkusHrPanacheImplementation") {
        extendsFrom( configurations.testImplementation )
    }
    named("quarkusHrPanacheRuntimeOnly") {
        extendsFrom( configurations.testRuntimeOnly )
    }
    named("quarkusHrPanacheCompileOnly") {
        extendsFrom( configurations.testCompileOnly )
    }

    named("jakartaDataImplementation") {
        extendsFrom( configurations.testImplementation )
    }
    named("jakartaDataRuntimeOnly") {
        extendsFrom( configurations.testRuntimeOnly )
    }
    named("jakartaDataCompileOnly") {
        extendsFrom( configurations.testCompileOnly )
    }
}

def quarkusOrmPanacheTest = tasks.register("quarkusOrmPanacheTest", Test) {
    description = "Runs the Quarkus ORM Panache tests."
    group = "verification"
    useJUnitPlatform()

    testClassesDirs = sourceSets.quarkusOrmPanache.output.classesDirs
    classpath = sourceSets.quarkusOrmPanache.runtimeClasspath
    shouldRunAfter( tasks.test )
}

def quarkusHrPanacheTest = tasks.register("quarkusHrPanacheTest", Test) {
    description = "Runs the Quarkus HR Panache tests."
    group = "verification"
    useJUnitPlatform()

    testClassesDirs = sourceSets.quarkusHrPanache.output.classesDirs
    classpath = sourceSets.quarkusHrPanache.runtimeClasspath
    shouldRunAfter( tasks.test )
}

def jakartaDataTest = tasks.register( "jakartaDataTest", Test ) {
    description = "Runs the Jakarta Data tests."
    group = "verification"
    useJUnitPlatform()

    testClassesDirs = sourceSets.jakartaData.output.classesDirs
    classpath = sourceSets.jakartaData.runtimeClasspath
    shouldRunAfter( tasks.test )
}

tasks.check {
    dependsOn( quarkusHrPanacheTest, quarkusOrmPanacheTest, jakartaDataTest )
}

tasks.test {
    dependsOn( quarkusHrPanacheTest, quarkusOrmPanacheTest, jakartaDataTest )
}

tasks.named( "sourcesJar" ) {
    dependsOn( ":hibernate-core:generateHqlParser", ":hibernate-core:generateSqlScriptParser" )
}

tasks.compileTestJava {
    options.compilerArgs.addAll(
            [
                    "-proc:none",
                    "-AsuppressJakartaDataMetamodel=true"
            ]
    )
}

def publishingExtension = extensions.getByType( PublishingExtension )
final String oldGroupId = "org.hibernate"
def oldArtifactId = "hibernate-jpamodelgen"

publishingExtension.publications.named( "groupRelocation", MavenPublication ) {
    artifactId = oldArtifactId
    pom {
        name.set("Relocation : $oldGroupId:$oldArtifactId -> ${project.group}:${project.name}")
        description.set("The `$oldArtifactId` module has been renamed `${project.name}` and moved to the `${project.group}` group-id")
    }
}

publishingExtension.publications.register( "renameRelocation", MavenPublication ) {
    artifactId = oldArtifactId
    pom {
        name.set("Relocation : ${project.group}:$oldArtifactId -> ${project.group}:${project.name}")
        description.set("The `$oldArtifactId` module has been renamed `${project.name}`")

        distributionManagement {
            relocation {
                groupId.set(project.group.toString())
                artifactId.set(project.name)
                version.set(project.version.toString())
            }
        }
    }
}

tasks.named( "forbiddenApisJakartaData" ) {
    enabled = false
}
tasks.named( "forbiddenApisQuarkusOrmPanache" ) {
    enabled = false
}
tasks.named( "forbiddenApisQuarkusHrPanache" ) {
    enabled = false
}
